# Review XXX-contracts du 13/10/2023

### R√©sum√©

La review du code du repo [XXX-contracts] a √©t√© faite depuis la branche [`contract/vesting`], au commit `XXX`.

La quantit√© de vuln√©rabilit√©s d√©tect√©es est la suivante :

| S√©v√©rit√© | D√©t√©ct√© |
|------------------|----|
| üö® Critical      |  1 |
| üî¥ High          |  0 |
| üü° Medium        |  4 |
| üü¢ Low           |  7 |
| üí¨ Informational |  3 |
| üõ†Ô∏è Optimization  |  4 |
| ‚ùì Question      |  2 |

-------

## XXX-OPT01: Utilisation d'un `require` redondant
**Severit√©:** `Optimization`
**Target:** `XXX.sol`

##### Description

La v√©rification du r√¥le `BURNER_ROLE` dans la fonction `burn()` se fait √† travers un `require()` qui confirme si l'appelant dans le contexte poss√®de le bon r√¥le.

```solidity
function burn(uint256 amount) public {
        // V√©rification que l'appelant a le r√¥le de br√ªleur
        require(hasRole(BURNER_ROLE, msg.sender), "Caller is not a burner");

        // Burn des tokens
        _burn(msg.sender, amount);
    }
```

Ce code est redondant et peut √™tre simplifi√© avec le modifier  `onlyRole()` de la librairie `AccessControl`.

##### Recommandation

Supprimer le [`require` de la ligne 41] et ajouter le modifier `onlyRole(BURNER_ROLE)` dans la signature de la fonction.

--------

## XXX-OPT02: Variable `immutable` non initialis√©e dans le `constructor`
**Severit√©:** `Optimization`
**Target:** `Vesting.sol`

##### Description

La variable `cliffPeriod` d√©finie comme `immutable` n'est pas initialis√©e dans le `constructor` du contrat `Vesting.sol`.

```solidity
uint256 public immutable cliffPeriod = 90 days;
```

##### Recommandation

Remplacer `immutable` par `constant` [√† la ligne 37].

--------

## XXX-OPT03: Variable `immutable`
**Severit√©:** `Optimization`
**Target:** `Vesting.sol`

##### Description

La variable `vestedToken` pourrait √™tre d√©finie comme `immutable` puisqu'elle est uniquement initialis√©e dans le `constructor` du contrat `Vesting.sol`.
```solidity
IERC20 public vestedToken;
```

##### Recommandation

Ajouter `immutable` [√† la ligne 47].

--------

## XXX-OPT04: Appel interne inutile
**Severit√©:** `Optimization`
**Target:** `VestingAggregator.sol`

##### Description

La fonction `removeVesting()` fait appel √† la fonction interne `vestingAddresses.contains()` afin de v√©rifier si le contrat de vesting √† supprimer du registre des vestings est bien pr√©sent ou non avant de le supprimer.
Cependant, cette v√©rification est rendonante puisqu'elle est d√©j√† effectu√© dans la fonction de suppression `vestingAddresses.remove()`.

```solidity
require(
    vestingAddresses.contains(vestingContract),
    "Vesting contract not found"
);
vestingAddresses.remove(vestingContract);
```

##### Recommandation

Remplacer [le contenu de la fonction `removeVesting()`] par:

```solidity
require(
    vestingAddresses.remove(vestingContract),
    "Vesting contract not found"
);
```

Dans le cas o√π le `vestingContract` √† supprimer n'existe pas dans le registre, `vestingAddresses.remove()` retournera une valeur bool√©ene `false` et le `require()` effectuera un `revert`.

--------

## XXX-INF01: Utilisation du `_setupRole` d√©pr√©ci√©
**Severit√©:** `Informational`
**Target:** `XXX.sol`

##### Description

La fonction `_setupRole()` du contrat OpenZeppelin `AccessControl` est maintenant d√©pr√©ci√©e.

```solidity
_setupRole(DEFAULT_ADMIN_ROLE, msg.sender);
```

##### Recommandation

Remplacer la fonction `_setupRole(DEFAULT_ADMIN_ROLE, msg.sender);` √† [la ligne 35] par `_grantRole(DEFAULT_ADMIN_ROLE, msg.sender);`.

--------

## XXX-INF02: Variable initialis√©e deux fois
**Severit√©:** `Informational`
**Target:** `Vesting.sol`

##### Description

La variable `totalVestingDuration` est configur√©e deux fois: 
La premi√®re fois dans le bytecode du contrat au moment du d√©ploiement avec une valeure par d√©faut √† `365 days`, et une deuxi√®me fois dans le `constructor` du contrat.

```solidity
uint256 public totalVestingDuration = 365 days;
...
constructor(address vestedTokenAddress, uint256 _totalVestingDuration) {
        vestedToken = IERC20(vestedTokenAddress);
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        
        if (_totalVestingDuration > 0) {
            totalVestingDuration = _totalVestingDuration;
        }
    }
```

##### Recommandation

Il est recommand√© de choisir une mani√®re claire de d√©finir la variable `totalVestingDuration` √† [la ligne 41](totalVestingDuration).

--------

## XXX-INF03: Entrypoint `transferOwnership()` non n√©cessaire
**Severit√©:** `Informational`
**Target:** `Vesting.sol`

##### Description

La fonction `transferOwnership(address newOwner)` permet de changer le propri√©taire du contrat. Pour ce faire, elle ajoute un nouveau administrateur et supprime l'administrateur actuel (l'adresse en contexte de l'appel). Cependant, bien que la fonction facilite l'appel de ces deux m√©thodes en une seule ex√©cution, il est aussi possible d'obtenir le m√™me r√©sultat en appelant les fonctions publiques h√©rit√©es par `AccessControl`.

```solidity
function transferOwnership(address newOwner)
        external
        onlyRole(DEFAULT_ADMIN_ROLE)
    {
        _grantRole(DEFAULT_ADMIN_ROLE, newOwner);
        _revokeRole(DEFAULT_ADMIN_ROLE, msg.sender);
    }
```

##### Recommandation

[L'entrypoint `transferOwnership(address newOwner)` √† la ligne 105] peut √™tre supprim√© du code.
Lors de la passation d'administration du contrat, il faudra alors effectuer deux appels distincts: 
d'abord un appel √† la fonction `grantRole(DEFAULT_ADMIN_ROLE, newOwner)` pour ajouter le nouvel administrateur, puis un second appel pour abandonner le r√¥le d'administrateur avec `renounceRole(DEFAULT_ADMIN_ROLE, msg.sender)`.

--------

## XXX-L01: Variables non utilis√©s
**Severit√©:** `Low`
**Target:** `XXX.sol`

##### Description

Des variables non utilis√©es sont pr√©sentes dans le code et peuvent pr√™ter √† confusion.
```solidity
uint8 public immutable burnPercentage = 2;
uint8 public immutable operationalFundPercentage = 2;
uint8 public immutable communityRewardTreasuryPercentage = 96;
```

##### Recommandation

Supprimer l'ensemble des variables inutilis√©s de [la ligne 14 √† la ligne 16].

--------

## XXX-L02: Events non utilis√©s
**Severit√©:** `Low`
**Target:** `XXX.sol`

##### Description

Des `events` non utilis√©s sont pr√©sents dans le code et peuvent pr√™ter √† confusion.

```solidity
event BurnerRoleGranted(address indexed burnerAddress);
event BurnerRoleRevoked(address indexed burnerAddress);
event OwnerRoleGranted(address indexed ownerAddress);
event OwnerRoleRevoked(address indexed ownerAddress);
event TokensDispersed(
    address indexed burnAddress,
    address indexed operationalFundAddress,
    address indexed communityRewardTreasuryAddress,
    uint256 totalBalance
);
```

##### Recommandation

Supprimer l'ensemble des `events` inutilis√©s de [la ligne 19 √† la ligne 28].

--------

## XXX-L03: Fonction inutile
**Severit√©:** `Low`
**Target:** `XXX.sol`

##### Description

La fonction `getContractBalance()` permet de r√©cup√©rer la balance de tokens ERC-20 d√©tenus dans le contrat. Cependant, il est d√©j√† possible d'effectuer cette action en utilisant l'entrypoint `balanceOf()` h√©rit√© du contrat `ERC20.sol`.

```solidity
function getContractBalance() public view returns (uint256) {
    return balanceOf(address(this));
}
```

##### Recommandation

Supprimer enti√®rement la fonction de [la ligne 54 √† la ligne 56]. 
Cette suppression permet d'√©conomiser l'ex√©cution d'un OPCODE `JUMP`.

--------

## XXX-L04: Fonction inutile (2)
**Severit√©:** `Low`
**Target:** `Vesting.sol`

##### Description

La fonction `getContractBalance()` permet de r√©cup√©rer la balance de tokens ERC-20 d√©tenus dans le contrat. Cependant, il est d√©j√† possible d'effectuer cette action en utilisant l'entrypoint `balanceOf()` du contrat `ERC20.sol` correspondant.

```solidity
function getContractBalance() public view returns (uint256) {
    return vestedToken.balanceOf(address(this));
}
```

##### Recommandation

Supprimer enti√®rement la fonction de [la ligne 250 √† la ligne 252]. 
Cette suppression permet d'√©conomiser l'ex√©cution d'un OPCODE `JUMP`.

--------

## XXX-L05: Variable non utilis√©e
**Severit√©:** `Low`
**Target:** `VestingAggregator.sol`

##### Description

Une variable non utilis√©e est pr√©sente dans le code et peut pr√™ter √† confusion.

```solidity
address[] public vestingContracts;
```

##### Recommandation

Supprimer la variable inutilis√© [`vestingContracts` de la ligne 12](vestingContracts), qui pr√™te √† confusion avec la variable `vestingAddresses`.

--------

## XXX-L06: Interface d√©pr√©ci√©e
**Severit√©:** `Low`
**Target:** `IVesting.sol`

##### Description

L'interface `IVesting.sol`, utilis√©e par le contrat `VestingAggregator.sol`, n'est pas √† jour et pr√©sente des fonctions d√©pr√©ci√©es.

##### Recommandation

Mettre √† jour l'interface [`IVesting.sol`].

--------

## XXX-L07: Le code est incorrectement format√© et indent√©
**Severit√©:** `Low`
**Target:** `Entire repository`

##### Description

Le code est incorrectement format√© et indent√© sur l'ensemble du projet.

##### Recommandation

Il est primordial de conserver un code correctement format√© et indent√©. Il est essentiel de d√©finir une configuration de formatage avec des outils tels que `prettier` ou `foundry` et de l'ex√©cuter syst√©matiquement avant chaque commit pour garantir une excellente qualit√© du code.

--------

## XXX-M01: La gestion des erreurs n'est pas uniforme
**Severit√©:** `Medium`
**Target:** `Entire repository`

##### Description

Le code m√©lange la gestion d'erreur √† travers des `require()` et des `custom errors`. Il est imp√©ratif de maintenir une coh√©rence dans la gestion de ces erreurs afin de faciliter la compr√©hension et la validation des chemins de r√©ussite (happy paths) et d'erreur (bad paths).

##### Recommandation

Il est fortement recommand√© de remplacer toutes les erreurs g√©n√©r√©es par des `require()` par des `custom errors`.

--------

## XXX-M02: Chemins d'erreur non atteints dans la couverture de test
**Severit√©:** `Medium`
**Target:** `Vesting.sol`

##### Description

Le sc√©nario dans lequel un utilisateur r√©clame ses tokens via l'entrypoint `claim()` avant l'ouverture du vesting n'a pas √©t√© test√© dans le projet (voir l'erreur √† [la ligne 135]).
```solidity
if (block.timestamp < startDateForCliff + cliffPeriod) {
    // 0% APRES REFLEXION JE SUIS OBLIGE DE LAISSER LA CONDITION
    revert VestingNotStarted();
}
```
Le sc√©nario dans lequel un utilisateur r√©clame ses tokens via l'entrypoint `claim()`, mais qu'aucun token n'est disponible pour lui, n'a pas √©t√© test√© dans le projet (voir l'erreur √† [la ligne 174]).
```solidity
} else {
    revert ClaimAmountIs0();
}
```


##### Recommandation

Il est fortement recommand√© d'ajouter des tests qui valident le fonctionnement de ces sc√©narios.

--------

## XXX-M03: Chemins non atteints dans la couverture de test
**Severit√©:** `Medium`
**Target:** `Vesting.sol`

##### Description

Plusieurs sc√©narios ont √©t√© laiss√©s de c√¥t√© dans la couverture de test.

```solidity
if (totalClaimed > totalVested) {
    metrics[2] = 0;
} else {
    metrics[2] = totalVested - totalClaimed; // Je suppose que c'est la d√©finition du "total lib√©r√©"
}

if(block.timestamp < startDateForCliff) {
    metrics[3] = startDateForCliff - block.timestamp; // Temps restant avant TLTC
} else {
    metrics[3] = 0;
}

uint256 vestingEndTime = startDateForCliff + cliffPeriod + totalVestingDuration;
if(block.timestamp < vestingEndTime) {
    metrics[4] = vestingEndTime - block.timestamp; // Temps restant avant TC
} else {
    metrics[4] = 0;
}
```

##### Recommandation

Il est fortement recommand√© d'ajouter un test qui valide le fonctionnement de tous ces sc√©narios manquants.

--------

## XXX-M04: Chemin non atteint dans la couverture de test
**Severit√©:** `Medium`
**Target:** `VestingAggregator.sol`

##### Description

Un sc√©nario a √©t√© laiss√© de c√¥t√© dans la couverture de test. Dans le cas o√π un utilisateur aurait des tokens disponibles suite √† l'appel agr√©g√© de la fonction `aggregatedClaim()`, la r√©clamation aupr√®s du contrat `Vesting.sol` associ√© n'a pas √©t√© test√©e ([voir ligne 50 √† 52]).
```solidity
if (claimableTokens != 0) {
    IVesting(vestingAddresses.at(j)).claim(msg.sender);
}
```

##### Recommandation

Il est fortement recommand√© d'ajouter un test qui valide le fonctionnement de ce sc√©nario manquant.

##### Remarque

Il est important de corriger des erreurs similaires dans l'ensemble du code, en plus de ce point soulev√©.

--------

## XXX-C01: Claim des tokens possible avant la fin du vesting
**Severit√©:** `Critical`
**Target:** `Vesting.sol`

##### Description

Lorsque le vesting d'un utilisateur est activ√©, il a actuellement la possibilit√© de r√©clamer la totalit√© de son vesting avant la date de fin du vesting. En effet, le calcul du vesting ne prend en compte que le montant total du vesting de l'utilisateur, sans consid√©rer les r√©clamations pr√©c√©demment effectu√©es. Par cons√©quent, un utilisateur malveillant peut effectuer autant d'appels que n√©cessaire d√®s le premier jour du vesting pour vider compl√®tement sa position verrouill√©e.

```solidity
    if (
            startDateForCliff + cliffPeriod + allocation.duration < block.timestamp
        ) {
            // 100 %
            newlyVested += allocation.amount;
        } else {
            // 100000 tokens  - 95% / (nb jours vesting - cliffPeriod) => 0.95 * 100K * 1/700
            uint256 durationVestedSoFar =
                block.timestamp - (startDateForCliff + cliffPeriod);
            /// @dev @audit Critical - A user can claim more tokens than he should.
            /// Since already claimed tokens are not substracted to the claimable amount, 
            /// a user can claim the same tokens ratio multiple times until his total allocation is depleted.
            newlyVested =
                (allocation.amount * durationVestedSoFar) / allocation.duration;
        }

        uint256 tmpTgeAmount = allocation.tgeAmount;

        if ((newlyVested + tmpTgeAmount) > 0) {
            uint256 oldAmount = allocation.amount;
            uint256 newAmount = oldAmount - newlyVested;
            uint256 duration = allocation.duration;

            userAddressToAllocation[_address] = Allocation({
                amount: newAmount,
                duration: duration,
                tgeAmount: 0 // forc√©ment 0
            });

            uint256 claimAmount = newlyVested + tmpTgeAmount;
            totalClaimed += claimAmount;

            vestedToken.safeTransfer(
                _address, claimAmount
            );
        }
```

##### Recommandation

Il est imp√©ratif de mettre √† jour le calcul des tokens r√©cup√©rables en prenant en compte soit la derni√®re date de `claim()` ou en prenant en compte le total de tokens d√©j√† r√©clam√©s sur l'ensemble du vesting, puis de soustraire ce montant au calcul actuel.

##### PoC de l'attaque

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

import "forge-std/Test.sol";
import "../../contracts/Vesting.sol";
import "../../contracts/XXX.sol";

contract VestingPoCTest is Test {
    Vesting internal vestingContract;
    XXX internal XXX;

    struct Allocation {
        uint256 duration;
        uint256 amount;
        uint256 tgeAmount;
    }

    function setUp() public {
        XXX = new XXX();
        vestingContract = new Vesting(address(XXX), 365 days);

        uint256 transferAmount = 500_000_000 * 10e18;
        XXX.transferFromContract(
            address(vestingContract),
            transferAmount
        );

        bytes32 MANAGER_ROLE = vestingContract.MANAGER_ROLE();
        vestingContract.grantRole(MANAGER_ROLE, address(this));
    }

    function test_ClaimingMultipleTimesAtOnce_scenario() public {
        address vester = makeAddr("vester");
        uint256 amount = 100_000 * 10e18;
        uint256 tgeAmount = (amount * 5) / 100; // 5% of amount

        vestingContract.startVesting();
        vestingContract.setAllocationToUser(
            vester,
            amount,
            100 days,
            tgeAmount
        );
        /// `cliffPeriod` is 90 days, so the user can't claim before 90 days.
        /// Vesting duration is 100 days, so the user can claim 1% of vesting each day.

        /// @dev Should not be able to claim before cliff period.
        vm.warp(block.timestamp + 89 days);
        vm.expectRevert();
        vestingContract.claim(vester);

        /// @dev The first day, the caller should get 0 tokens from his vesting,
        /// But he should get his TGE amount.
        vm.warp(block.timestamp + 1 days);
        vestingContract.claim(vester);
        /// @dev Claiming again should revert, since the user already claimed his TGE amount.
        vm.expectRevert();
        vestingContract.claim(vester);

        console.log("Vester balance before claiming vesting is: %s", XXX.balanceOf(vester));

        vm.warp(block.timestamp + 10 days);
        (, uint256 leftAmount, ) = vestingContract.userAddressToAllocation(
            vester
        );
        /// @dev In theory, the user should only claim 10% of his vesting at this point.
        /// However, as long as the user has tokens left in his vesting, he can claim them, 10% at a time.
        while (leftAmount > 10) {
            vestingContract.claim(vester);
            (, leftAmount, ) = vestingContract.userAddressToAllocation(vester);
        }
        console.log("Vester balance after spamming vesting claim is: %s", XXX.balanceOf(vester));
        console.log("Tokens left to claim is: %s", leftAmount);
    }
}
```

--------

## XXX-Q01: Quelle strat√©gie sera adopt√©e si un vesting a √©t√© mal configur√© ?
**Severit√©:** `Question`
**Target:** `Vesting.sol`

##### Description

Lorsqu'un administrateur configure le vesting d'un utilisateur via [l'entrypoint `setAllocationToUser()`], il n'est actuellement pas possible de le modifier ult√©rieurement. Est-ce que ce m√©canisme sera maintenu pour la suite du projet, ou serait-il pr√©f√©rable d'ajouter un setter par pr√©caution ?

--------

## XXX-Q02: Pourquoi un `totalVestingDuration` est mis en place dans le contrat ?
**Severit√©:** `Question`
**Target:** `Vesting.sol`

##### Description

Lorsqu'un administrateur configure le vesting d'un utilisateur via [l'entrypoint `setAllocationToUser()`],  il d√©termine la dur√©e du vesting de l'utilisateur en utilisant la variable `_duration`.
Cependant, se pose la question de savoir si cette configuration devrait √™tre maintenue, √©tant donn√© que la variable `totalVestingDuration` d√©finie au niveau global du contrat est disponible et n'est pas utilis√©e ?
